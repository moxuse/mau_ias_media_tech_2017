# 後期映像表現演習2-3

## ビデオエフェクト

ビデオの入力を取得できたところで、今度はフレームごとのピクセル情報にアクセスして、それを加工してみましょう。
取得できるのは各フレームのRGBの配列情報です。これに手を加えてビデオエフェクターのようなものを作ってみましょう。

まずは単純にカメラのピクセル情報にアクセスしてみましょう。


```
import processing.video.*;
 
Capture cam;
 
void setup() {
  size(640, 480);
 
  cam = new Capture(this, 640, 360, "FaceTime HDカメラ（内蔵）");
  cam.start();
}
 
void draw() {
  if (cam.available()) {
    cam.read();
    image(cam, 0, 0);
  }
}
```

imageで書き込めるとがわかりました、このように画像データと同じような扱いでビデオのピクセル情報を利用できそうです。
では```PImage```にピクセル情報を格納してみましょう。

### ピクセルデータの取得

まずは格納先の```PImage```を用意します。今回は画像ファイルからロードしないので```createImage```を使って初期化しておきましょう。

```my_image = createImage(640, 480, RGB);```

これで空の状態のPImageを用意できました。

ビデオのフレームデータが揃うタイミングを```cam.available()```で取得して新しいフレームが入ってきたかどうかをチェックしましょう。
描画のフレームのアップデートと、ビデオのフレームの読み込みは必ずしも同じタイミングではありません。もしまだビデをのデータが揃っていないなら、
ピクセルへのアクセスはスキップするようにします。

```cam.read();```

準備ができたら、PImageの時のようにforループを使って各ピクセルにアクセスしていきます。


```
for (int i = 0; i < cam.height; i ++) {
    for (int j = 0; j < cam.width; j ++) {
        int loc = i * width + j;
        color color_ = cam.pixels[loc];
    }
}
```

```cam.pixels```

Capture クラスの```pixsel```にアクセスます。ここで注意したいのが、ここで帰ってくる配列が、ピクセルの二次元配列ではなく、一次元のArrayということです。
それぞれのピクセルの位置はCaptureのサイズで指定したwithとheightで自分で揃えてあげる必要があります。

![pixel_2](図版/pixel_2.png)

```int loc = i * width + j;```


ピクセルのカラー情報が取得できたら同じループ内で、同時に用意したPImageのピクセルに格納してしまいましょう。

PImageには```.pixel```でピクセルの配列にカラーを直接指定することができます。
指定し終わったら```updatePixels()```関数でピクセルの情報が更されたことを```PImage```に伝えます。


```my_image.pixels[i * cam.width + j] = color_;```

```my_image.updatePixels();```


### コード全体

```
import processing.video.*;
 
Capture cam;
color colors[];

PImage my_image; 

void setup() {
  size(640, 480);
  cam = new Capture(this, 640, 480, "FaceTime HD Camera");
  my_image = createImage(640, 480, RGB);
  loadPixels();
  cam.start();
}
 
void draw() {
  if (cam.available()) {
    cam.read();
 
    for (int i = 0; i < cam.height; i ++) {
      for (int j = 0; j < cam.width; j ++) {
        int loc = i * cam.width + j;
        
        color color_ = cam.pixels[loc];
 
        my_image.pixels[i * cam.width + j] = color_;
      }
    }
    my_image.updatePixels();
    
    image(my_image, 10, 10);
  }
}
```

### tint

```tint```関数で手軽にピクセルのカラー操作を行えます。tintは```colorMode```によって指定する値の意味が変わります。

```RGB```の場合は red, blur, green の成分を

```HSB```の時は hue, saturation, brightness の成分を操作します。

デフォルトではcolorModeは```RGB```になっています。


```
    my_image.updatePixels();
    
    tint(155, 20, 240);

    image(my_image, 10, 10);
```

alphaの値を操作してディレイのような効果。

```
tint(255, mouseX / (float)width * 255.0);
```

![video_cap_tint.png](図版/video_cap_tint.png)


### ピクセルごとの操作

ピクセルが格納できたので、試しに前回使用したモザイク処理を入れてみましょう。
前回PImage使ったコードを改変して、ピクセルの情報で```rect()```ね塗りつぶして、タイル状に敷き詰めてみます。


### コード全体

```
import processing.video.*;
 
Capture cam;
color colors[];

PImage mosaic_image;
int division = 40;

int scale_x, scale_y;

color[][] target_colors = new color[division][division];

void setup() {
  size(640, 480);
  cam = new Capture(this, 640, 480, "FaceTime HD Camera");
  mosaic_image = createImage(640, 480, RGB);

  scale_x = mosaic_image.width / division;
  scale_y = mosaic_image.height / division;

  loadPixels();
  cam.start();
}
 
void draw() {
  if (cam.available()) {
    cam.read();
 
    for (int i = 0; i < cam.height; i ++) {
      for (int j = 0; j < cam.width; j ++) {
        int loc = i * cam.width + j;
        
        color color_ = cam.pixels[loc];
 
        mosaic_image.pixels[i * cam.width + j] = color_;
      }
    }
    mosaic_image.updatePixels();


    for (int i = 0; i< division; i++) {
      for (int j = 0; j< division; j++) {
        target_colors[j][i] = mosaic_image.get(j * scale_x, i * scale_y);
      }
    }

    fill(255);
    rect(0,0, width, height);

    for (int i = 0; i < division; i++) {
      for (int j = 0; j< division; j++) {
        fill(target_colors[j][i]);
        noStroke();

        pushMatrix();
          rect(0, 0, 10, 10);
        popMatrix();

        translate(10, 0);
        if (j == division - 1) {
          translate(-(division * 10), 0);
        }
      }
      translate(0, 10);
    }
  }
}
```
